fs = require 'fs'
nodefs = require 'node-fs'
walk = require 'walk'
require 'flour'
path = require 'path'
util = require 'util'
require 'colors'

coffeepath = 'coffee'
jspath = 'js/src'

buildCoffee = (source) ->
    destDir = path.dirname source.replace(coffeepath, jspath)
    mkdir destDir
    basename = path.basename source, '.coffee'
    if path.extname(source) == '.coffee'
        compile source, path.join(destDir, basename + '.js')
    else
        fs.createReadStream(source).pipe(fs.createWriteStream path.join(destDir, basename))

mkdir = (destDir) ->
    if not fs.existsSync destDir
        # recursively create the directory
        nodefs.mkdirSync destDir, '0755', true
        
walkDir = (srcDir, fn) ->
    walker = walk.walkSync srcDir
    walker.on 'file', (root, fileStat, next) ->
        fn path.join(root, fileStat.name)
        next

task 'build:coffee', ->
    walkDir coffeepath, (source) -> buildCoffee source

task 'build:less', ->
    compile 'less/style.less', 'css/style.css'

task 'build', ->
    invoke 'build:coffee'
    invoke 'build:less'

task 'watch', ->
    invoke 'build'
    walkDir coffeepath, (source) -> watch source, ->  buildCoffee source

    watch 'less/*.less', ->  invoke 'build:less'

task 'lint', 'Check javascript syntax', ->
    lint 'js/*.js'